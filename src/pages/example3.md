---
title: RNA-Seq pipeline
layout: "@layouts/MarkdownPage.astro"
---

<div class="blg-summary example">
<h3>RNA-Seq pipeline</h3>

<p class="text-muted">
    This example shows how to put together a basic RNA-Seq pipeline.
</p>

```groovy
#!/usr/bin/env nextflow

/*
 * Pipeline parameters
 */

// Input data
params.reads = "${projectDir}/data/ggal/ggal_gut_{1,2}.fq"

// Reference file
params.transcriptome = "${projectDir}/data/ggal/ggal_1_48850000_49020000.Ggal71.500bpflank.fa"

// Output directory
params.outdir = "results"

process INDEX {
    tag "$transcriptome.simpleName"

    input:
    path transcriptome

    output:
    path 'index'

    script:
    """
    salmon index --threads $task.cpus -t $transcriptome -i index
    """
}

process FASTQC {
    tag "FASTQC on $sample_id"
    publishDir params.outdir

    input:
    tuple val(sample_id), path(reads)

    output:
    path "fastqc_${sample_id}_logs"

    script:
    """
    fastqc.sh "$sample_id" "$reads"
    """
}

process QUANT {
    tag "$pair_id"
    publishDir params.outdir

    input:
    path index
    tuple val(pair_id), path(reads)

    output:
    path pair_id

    script:
    """
    salmon quant --threads $task.cpus --libType=U -i $index -1 ${reads[0]} -2 ${reads[1]} -o $pair_id
    """
}

workflow {

    // Paired reference data
    read_pairs_ch = channel.fromFilePairs( params.reads, checkIfExists: true )

    // Index reference transcriptome file
    INDEX(params.transcriptome)

    // Generate FastQC reports
    FASTQC(read_pairs_ch)

    // Quantify reads
    QUANT(INDEX.out, read_pairs_ch)
}
```

</div>

### Synopsis

This example shows a basic Nextflow pipeline consisting of three processes. The `INDEX` process creates index files for the input BAM files. The `FASTQC` process takes the index bam files created by the `SAMTOOLS_INDEX` process and accessory files and creates variant call files. Finally, the `GATK_JOINTGENOTYPING` process consolidates the variant call files generated by `GATK_HAPLOTYPECALLER` and applies a joint genotyping analysis.

### Try it

This pipeline is available on the [nextflow-io/rnaseq-nf](https://github.com/nextflow-io/rnaseq-nf) GitHub repository.

An active internet connection and Docker are required for Nextflow to download the pipeline and necessary Docker images and run the pipeline within containers. The data used by this pipeline is stored on the GitHub repository and will download automatically.

To try this pipeline:

1. Follow the [Nextflow installation guide](https://www.nextflow.io/docs/latest/install.html#install-nextflow) to install Nextflow.
2. Follow the [Docker installation guide](https://docs.docker.com/get-started/get-docker/) to install Docker.
3. Launch the pipeline:

   nextflow run nextflow-io/rnaseq-nf -with-docker

**NOTE**: The `rnaseq-nf` pipeline on GitHub is under active development and may differ from the example shown above.
